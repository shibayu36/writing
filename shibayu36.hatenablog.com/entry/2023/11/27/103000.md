---
Title: fzfを使ってgit stashを便利に扱えるように
Category:
- tech
Date: 2023-11-27T10:30:00+09:00
URL: https://blog.shibayu36.org/entry/2023/11/27/103000
EditURL: https://blog.hatena.ne.jp/shiba_yu36/shibayu36.hatenablog.com/atom/entry/6801883189061887748
---

git stashをもっと便利に扱いたいと思い、fzfを使って使いやすくしてみた。以下のURLに載っているものを参考にして自分にとって使いやすいように改変した。

- https://zenn.dev/shimo_s3/articles/6225dfc118c49d:title
- [git-stash-explore](https://github.com/bturrubiates/fzf-scripts/blob/master/git-stash-explore)

### できたこと
- 今の変更ファイルをfzfを使って選択して、選択したものだけをstash (git-stash-select)
- stash一覧の中から中身をpreviewしながら選び、apply or deleteする (git-stashes)

<iframe width="560" height="315" src="https://www.youtube.com/embed/3zZ2rECZOMs?si=-oSEJrgiO-5DGt-E" title="YouTube video player" frameborder="0" allow="accelerometer; autoplay; clipboard-write; encrypted-media; gyroscope; picture-in-picture; web-share" allowfullscreen></iframe>

### 現在の変更ファイルから一部を選んでgit stashするコマンド
https://zenn.dev/shimo_s3/articles/6225dfc118c49d:title を参考に、[git-stash-select](https://github.com/shibayu36/config-file/blob/d4e35204a5d83f58093a674328769cbd9929524a/bin/git-stash-select)というコマンドを作った。

```zsh
#!/usr/bin/env bash

# Get the root directory of the Git repository
git_repo_root=$(git rev-parse --show-toplevel)

# Add instructions to the list of modified files
instructions="Select multiple files by TAB, and then press Enter to stash them."

# Use fzf to select multiple files
selected_files=$(git status --porcelain |
                 fzf --multi \
                     --header="$instructions" \
                     --preview-window='down:70%' \
                     --preview="
                        if [[ {} =~ '^\?\?' ]]; then
                          cat $git_repo_root/{2};
                        else
                          git -C $git_repo_root diff --color=always {2};
                        fi
                     " |
                 awk '{ print $2 }'
                )

# Check if any files were selected
if [ -z "$selected_files" ]; then
    echo "No files selected. Exiting."
    exit 1
fi

# Store the selected files in an array
IFS=$'\n' read -rd '' -a files_array <<<"$selected_files"

# Filter out the instructions from the array
filtered_files_array=()
for file in "${files_array[@]}"; do
    if [[ $file != *"==="* ]]; then
        filtered_files_array+=("$file")
    fi
done

# Prompt for a stash message
echo "Enter a stash message:"
read stash_message

# Stash the selected files
git -C $git_repo_root stash push -u -m "$stash_message" -- "${filtered_files_array[@]}"
```

工夫ポイントはpreviewの部分だ。git diffなどはUntrackedなファイルの内容表示に弱く、そのままだと表示できない。そのためUntrackedな場合はcatを使うなどの場合分けをしている。

### stash一覧の中から選んでapply or deleteするコマンド
[git-stash-explore](https://github.com/bturrubiates/fzf-scripts/blob/master/git-stash-explore) を参考に、[git-stashes](https://github.com/shibayu36/config-file/blob/d4e35204a5d83f58093a674328769cbd9929524a/bin/git-stashes)というコマンドを作った。

```zsh
#!/usr/bin/env zsh

while out=$(git stash list "$@" |
            fzf --ansi --no-sort --reverse --print-query --query="$query" \
                --expect=enter,bspace \
                --bind="ctrl-space:preview-page-up" \
                --bind="space:preview-page-down" \
                --bind="k:preview-up" \
                --bind="j:preview-down" \
                --preview="echo {} | cut -d':' -f1 | xargs -I {STASH} sh -c 'git stash show --color=always -p {STASH}; git show --color=always --format="" -p {STASH}^3'" \
                --preview-window='down:85%');
do
    # Tokenize selection by newline
    selection=("${(f)out}")

    # Keep the query accross fzf calls
    query="$selection[1]"
    # Represents the key pressed
    key="$selection[2]"
    # Represents the stash, e.g. stash{1}
    reflog_selector=$(echo "$selection[3]" | cut -d ':' -f 1)

    case "$key" in
        # enter applies the stash to the current tree
        enter)
            git stash apply "$reflog_selector"
            break
            ;;
        # backspace will drop the stash
        bspace)
            git stash drop "$reflog_selector"
            ;;
    esac
done
```

こちらもUntrackedなファイル対策をしている。https://stackoverflow.com/questions/12681529/in-git-is-there-a-way-to-show-untracked-stashed-files-without-applying-the-stas:title によると、あるstashの中のUntrackedなファイルを表示するには `git show 'stash@{0}^3'`のように`^3`をつければ良いらしい。そこで`git stash show stash@{0}`と`git show stash@{0}^3`の両方を実行することで、stashの中のModifiedとUntrackedの両方を出力するようにした。

applyとdeleteについては、enterとbackspaceが直感的と感じたので、その2つにキーを割り当てている。

またstashの中身を見るときにpreviewのスクロールをしたい。このコマンドは文字列検索をほぼすることがないなと思ったので、tigなどに合わせてjでscroll down、kでscroll upとなるようにした。

### まとめ
今回はgit stashをfzfでもっと便利に扱えるように、変更ファイルを選択してgit stashするコマンドと、stashの中から選択してapplyやdeleteをするコマンドを紹介した。便利なので良かったら使ってほしい。
