---
Title: anything-git-project.elを少し手直ししてみた
Category:
- tech
- emacs
Date: 2012-12-22T13:51:57+09:00
URL: https://blog.shibayu36.org/entry/2012/12/22/135157
EditURL: https://blog.hatena.ne.jp/shiba_yu36/shibayu36.hatenablog.com/atom/entry/12704830469096659490
---

　id:yaottiさんが作っているanything-git-project.el([http://yaotti.hatenablog.com/entry/20101216/1292500323:title])ですが、gitでproject管理している人にとってはすごく便利です。
　ただ、一つだけ不満がありました。
- 現在のディレクトリからの相対パスが出てしまう
-- ../../とかあんまり見たくない

　そこでちょっとだけ手直ししてみました。

* chomp
　elisp触ってるとchompが欲しいけど無い、みたいな状況に出会います。そこで以下の関数をユーティリティとして定義しておくと良いと思います。今回の手直しではこれを使ってるので、適当にどこかで定義しておいてください。

>|lisp|
(defun chomp (str)
  (replace-regexp-in-string "[\n\r]+$" "" str))
||<

* 変更したもの
>|lisp|
(defun anything-git-project-project-dir ()
  (chomp
   (shell-command-to-string "git rev-parse --show-toplevel")))

(defun anything-c-sources-git-project-for ()
  (loop for elt in
        '(("Modified files (%s)" . "--modified")
          ("Untracked files (%s)" . "--others --exclude-standard")
          ("All controlled files in this project (%s)" . ""))
        collect
        `((name . ,(format (car elt) (anything-git-project-project-dir)))
          (init . (lambda ()
                    (unless (and ,(string= (cdr elt) "") ;update candidate buffer every time except for that of all project files
                                 (anything-candidate-buffer))
                      (with-current-buffer
                          (anything-candidate-buffer 'global)
                        (insert
                         (shell-command-to-string
                          ,(format "git ls-files --full-name $(git rev-parse --show-cdup) %s"
                                   (cdr elt))))))))
          (candidates-in-buffer)
          (display-to-real . (lambda (name)
                               (format "%s/%s"
                                       (anything-git-project-project-dir) name)))
          (type . file))))

(defun anything-git-project ()
  (interactive)
  (let* ((sources (anything-c-sources-git-project-for)))
    (anything-other-buffer sources
                           (format "*Anything git project in %s*"
                                   (anything-git-project-project-dir)))))
||<

　これを使えば以下のように../とかの相対パスではなく、project rootからの相対パスで表示され、移動することができます。以下のような感じ
[f:id:shiba_yu36:20121222132735p:plain]


* いくつかの技術的tips
** gitでprojectのrootディレクトリを取得する
これは簡単で
>|sh|
git rev-parse --show-toplevel
||<
を実行すれば良いです。最後に改行文字が入ってくるので注意。

** gitで管理されているファイルをrootからの相対パスで取得する
これも簡単で
>|sh|
git ls-files --fullname
||<
としてやれば取得できます。

更に今回はどのディレクトリにいても全ファイル必要なので
>|sh|
git ls-files --full-name $(git rev-parse --show-cdup)
||<
としてあげます。

show-cdupは現在のディレクトリからrootディレクトリまでの相対パスをくれます。../../みたいなやつ。

** anything上で見えているものと実行するものを変える
　今回現在のディレクトリからの相対パスではなく、project rootからの相対パスを表示するようにしたため、anythingのtype fileでそのまま実行しようとすると違うファイルが選択されてしまいます。そこでanythingのdisplay-to-realを利用して、絶対パスへの書き換えを行なってあげます。
　以下の部分が該当しているところ。anything-git-project-project-dirが現在のproject root dirを返してくれるので、それにrootからの相対パスを足してあげて返します。これでちゃんとしたファイルが選択できるようになりました。
>|lisp|
(display-to-real . (lambda (name)
                    (format "%s/%s" (anything-git-project-project-dir) name)))
||<

* 最後に
　今回の変更をするにあたり、anything周りのことはEmacsテクニックバイブルが参考になりました。
[asin:4774143278:detail]

　elisp周りまだぜんぜんわかってないので、Emacs Lispテクニックバイブルも買いたいなー。
[asin:4774148970:detail]
